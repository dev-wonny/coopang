plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.4'
    id 'io.spring.dependency-management' version '1.1.6'
}

group = 'com.coopang'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

ext {
    set('springCloudVersion', "2023.0.3")
    set('querydslVersion', "5.0.0")
}

dependencies {
    // multi module
    implementation project(':DATA:core-data')
    implementation project(':DATA:api-data')
    implementation project(':INFRA:api-config')
    implementation project(':INFRA:api-communication')

    // actuator
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    // validation
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.projectlombok:lombok'

    // Prometheus for metrics monitoring
    implementation 'io.micrometer:micrometer-registry-prometheus'

    // Loki Logback Appender for logging to Loki
    implementation 'com.github.loki4j:loki-logback-appender:1.5.1'

    // Swagger for API documentation
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.0.2'

    // for web
    implementation 'org.springframework.boot:spring-boot-starter-web'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    // for eureka
    implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'

    // Database
    implementation 'org.postgresql:postgresql'

    // for JPA
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
//	implementation 'org.springframework.boot:spring-boot-starter-hateoas'

    // for Spring security
    implementation 'org.springframework.boot:spring-boot-starter-security'
    testImplementation 'org.springframework.security:spring-security-test'

    // BCrypt for password hashing
    implementation 'org.springframework.security:spring-security-crypto'

    // JWT for authentication
    implementation group: 'io.jsonwebtoken', name: 'jjwt-api', version: '0.11.5'
    runtimeOnly group: 'io.jsonwebtoken', name: 'jjwt-impl', version: '0.11.5'
    runtimeOnly group: 'io.jsonwebtoken', name: 'jjwt-jackson', version: '0.11.5'
    //implementation 'io.jsonwebtoken:jjwt:0.12.6'

    // QueryDSL
    implementation "com.querydsl:querydsl-jpa:${querydslVersion}:jakarta"
    annotationProcessor "com.querydsl:querydsl-apt:${querydslVersion}:jakarta"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"
    implementation 'com.github.gavlyukovskiy:p6spy-spring-boot-starter:1.9.1'

    // cache
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'com.github.ben-manes.caffeine:caffeine'

    // Testing dependencies
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // openfeign
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'

    // Mapper
    implementation 'org.modelmapper:modelmapper:3.1.0'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

def querydslSrcDir = 'src/main/generated'
clean {
    delete file(querydslSrcDir)
}
